/**
 * Copyright (C) 2016 Dr. David H. Akehurst (http://dr.david.h.akehurst.net)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
grammar net.akehurst.example.xtext.vertx.language.Language hidden(WS, ML_COMMENT, SL_COMMENT)

generate math "http://net.akehurst/example/xtext/vertx/language/Math"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Package: 
	
	'package' name=ID '{'
	
		types+=TypeDecl*
	
		instances+=Instance*
	
	'}'	
;


TypeDecl:
	PrimitiveType
	| EnumType
	| EntityType
;

PrimitiveType : 'primitive' name=ID;

EnumType:
	'enum' name=ID '{'
		literals+=EnumLiteral+
	'}'
;

EnumLiteral :
	name=ID
;

EntityType :
	'entity' name=ID '{'
		propertyDefs+=PropertyDef*
	'}'
;

PropertyDef : 
	name=ID ':' type=[TypeDecl]
;

Instance:
	PrimitiveInstance
	| EnumInstance
	| EntityInstance
;

PrimitiveInstance :
	type=[PrimitiveType] '(' value=STRING ')'
;

EnumInstance :
	type=[EnumType] '.' value=[EnumLiteral]
;


EntityInstance:
	type=[EntityType] '('
		properties+=Property*
	')'
;

Property:
	name=ID '=' value=Instance
;

terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;
terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal STRING: '"' -> '"' ;
terminal INT returns ecore::EInt: ('0'..'9')+;
terminal ID: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')* ;

